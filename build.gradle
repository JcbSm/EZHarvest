plugins {
    id 'java'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

group = 'com.github.jcbsm'
version = '2.1'

repositories {
    mavenCentral()
    maven {
        name = "papermc-repo"
        url = "https://repo.papermc.io/repository/maven-public/"
    }
    maven {
        name = "sonatype"
        url = "https://oss.sonatype.org/content/groups/public/"
    }
}

dependencies {
    compileOnly "io.papermc.paper:paper-api:1.20.1-R0.1-SNAPSHOT"
}

shadowJar {
    archiveClassifier = ''
}

def targetJavaVersion = 17
java {
    def javaVersion = JavaVersion.toVersion(targetJavaVersion)
    sourceCompatibility = javaVersion
    targetCompatibility = javaVersion
    if (JavaVersion.current() < javaVersion) {
        toolchain.languageVersion = JavaLanguageVersion.of(targetJavaVersion)
    }
}

tasks.withType(JavaCompile).configureEach {
    if (targetJavaVersion >= 10 || JavaVersion.current().isJava10Compatible()) {
        options.release = targetJavaVersion
    }
}

processResources {
    def props = [version: version]
    inputs.properties props
    filteringCharset 'UTF-8'
    filesMatching('plugin.yml') {
        expand props
    }
}

tasks.register('jarCopy') {
    group 'testsrv'
    dependsOn 'shadowJar'
    doLast {
        copy {
            from 'build/libs/' + rootProject.name + '-' + project.version + '.jar'
            into 'testsrv/plugins'
        }
    }
}

tasks.register('runServer') {
    group 'testsrv'
    doLast {
        exec {
            workingDir "${projectDir}/testsrv/"
            commandLine 'java', '-jar', 'paper-1.20.1.jar', '-nogui'
        }
    }
}

tasks.register('buildRunServer') {
    group 'testsrv'
    dependsOn 'jarCopy', 'runServer'
}


